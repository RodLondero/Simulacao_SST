Clear

var @VL = (0.22 3 sqrt *)
var @var_linecode = 2CA_3 !2CA_3

/* ------------------------------------------------------------------------------------------------------- */
/*  Subestacao
/* ------------------------------------------------------------------------------------------------------- */
New Circuit.Fonte bus1=Barra1 phases=3 basekv=115 pu=1 MVAsc3=21e3 MVAsc1=20e3

New Transformer.Subestacao phases=3 windings=2   xhl=1e-12 sub=y
~ wdg=1 bus=Barra1  conn=delta  kV=115    kVA=1000   %r=0.0005
~ wdg=2 bus=Barra2  conn=delta  kV=13.8   kVA=1000   %r=0.0005

/* ------------------------------------------------------------------------------------------------------- */
/*  LineCodes e LoadShapes
/* ------------------------------------------------------------------------------------------------------- */
Redirect Biblioteca/LineCodes2.dss
Redirect Biblioteca/LoadShapes.dss

/* ------------------------------------------------------------------------------------------------------- */
/*  Linhas
/* ------------------------------------------------------------------------------------------------------- */
New Line.Linha1 phases=3 bus1=Barra2 bus2=Barra3 length=20 units=km linecode=@var_linecode
New Line.Linha2 phases=3 bus1=Barra3 bus2=Barra4 length=20 units=km linecode=@var_linecode

/* ------------------------------------------------------------------------------------------------------- */
/*  Cargas
/* ------------------------------------------------------------------------------------------------------- */
New Load.Carga1  Bus1=Barra3 Phases=3 Conn=Wye  Model=1  kV=13.8  kW=100 kvar=0 Enabled=No
New Load.Carga   Bus1=Barra5 Phases=3 Conn=Wye  Model=1  kV="@VL" kw=150 kvar=0 daily=Residencial_Minuto

/* ------------------------------------------------------------------------------------------------------- */
/*  Trafo
/* ------------------------------------------------------------------------------------------------------- */
New Transformer.TR phases=3 windings=2 xhl=1e-10 Enabled=No
~ wdg=1 bus=Barra4      conn=delta  kV=13.8   kVA=200   %r=5e-6
~ wdg=2 bus=Barra5      conn=wye    kV="@VL"  kVA=200   %r=5e-6

/* ------------------------------------------------------------------------------------------------------- */
/*  SST
/* ------------------------------------------------------------------------------------------------------- */
New VSource.FonteSST_Carga  Bus1=Barra5  Phases=3 basekv="@VL"
New Load.CargaSST_Carga     Bus1=Barra4  Phases=3 Conn=Wye Model=1 kV=13.8  kw=150 kvar=0 daily=Residencial_Minuto

New Monitor.Potencia_FonteSST element=VSource.FonteSST_Carga  terminal=1 mode=1  ppolar=no
New Monitor.Potencia_CargaSST element=Load.CargaSST_Carga     terminal=1 mode=1  ppolar=no

/* ------------------------------------------------------------------------------------------------------- */
/*  PVSystem
/* ------------------------------------------------------------------------------------------------------- */
New XYCurve.MyPvsT    npts=4  xarray=[0 25 75 100]  yarray=[1.2 1.0 0.8  0.6]
New XYCurve.MyEff     npts=4  xarray=[.1 .2 .4 1.0] yarray=[.86 .9 .93 .97]

New Loadshape.MyIrrad npts=1440 interval=1 mult=(file=Biblioteca/Irradiacao_24.csv)
New Tshape.MyTemp     npts=1440 interval=1 temp=(file=Biblioteca/Temperatura_24.csv)

New PVSystem.FV phases=3 bus1=Barra5 kV="@VL" kVA=250 PF=1 Pmpp=200
~ irrad=0.9 temperature=25 %cutin=0.1 %cutout=0.1
~ effcurve=Myeff
~ P-TCurve=MyPvsT
~ Daily=MyIrrad
~ TDaily=MyTemp !default for inverter is unity PF

New Monitor.FV_Power  element=PVSystem.FV  terminal=1 mode=1 ppolar=no
New Monitor.FV        element=PVSystem.FV  terminal=1 mode=3

/* ------------------------------------------------------------------------------------------------------- */
/*  Medidores e Monitores
/* ------------------------------------------------------------------------------------------------------- */
New EnergyMeter.Subestacao element=Transformer.Subestacao  terminal=1

Redirect Medidores/Monitores.dss

Set VoltageBases = [115, 13.8, "@VL"]
CalcVoltageBases

/* ------------------------------------------------------------------------------------------------------- */
/*  Solve
/* ------------------------------------------------------------------------------------------------------- */
var @usa_sst = No
var @usa_tr  = Yes

PVSystem.FV.Enabled            = Yes
VSource.FonteSST_Carga.Enabled = @usa_sst
Load.CargaSST_Carga.Enabled    = @usa_sst
Transformer.TR.Enabled         = @usa_tr

Set MaxControlIter = 100
Set MaxIterations = 100
Set ControlMode=OFF
Set Mode=Daily StepSize=1h hour=15 number=5
Solve

Plot monitor object= potencia_subestacao


//Plot monitor object= tensao_subestacao channels=(1 3 5 ) bases = ("115000 3 sqrt /" "115000 3 sqrt /" "115000 3 sqrt /")
//Plot monitor object= Tensao_Carga      channels=(1 3 5 ) bases=(220 220 220)
//Plot monitor object= Potencia_Carga    channels=(1 3 5 )
//Plot loadshape object = Residencial_Minuto

//Plot LoadShape object = MyIrrad

Show Powers kva Elements
//Show Voltage LN Nodes
//Show Voltage LN Elements

Summary

